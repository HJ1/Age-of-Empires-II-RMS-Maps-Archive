/* RMS Entry "Burning Plains" by Mobius One */
/* Notes: Must be made Applicable for 3v3 and 4v4 games (medium and large map sizes). No DLC content: strictly AoC. */

/* Edited by HJ/HJFE for Clan Masters */

<PLAYER_SETUP>
random_placement

<LAND_GENERATION>

start_random
  percent_chance 50
  #define DESERT_MAP
end_random

if DESERT_MAP
  base_terrain                     DESERT
else           
  base_terrain                     DIRT
endif

create_land
{
terrain_type  				WATER
land_percent 				100
left_border 				0
right_border 				90
top_border 				0
bottom_border 				0 
clumping_factor 			8
}

create_land
{
terrain_type  				WATER
land_percent 				100
left_border 				0
right_border 				0
top_border 				0
bottom_border 				90 
clumping_factor 			8
}

create_player_lands
{
if DESERT_MAP 
terrain_type  				DESERT
else
terrain_type  				DIRT
endif
land_percent 				10
left_border 				15
if LARGE_MAP
else
right_border				10
endif
top_border				15
bottom_border				10
base_size                           9
  border_fuzziness                  15
}

<TERRAIN_GENERATION>

create_terrain				DEEP_WATER
{
base_terrain				WATER
land_percent				100
spacing_to_other_terrain_types		12
left_border 				0
right_border 				95
top_border				0
bottom_border				0
set_avoid_player_start_areas
clumping_factor				4
}

create_terrain				DEEP_WATER
{
base_terrain				WATER
land_percent				100
spacing_to_other_terrain_types		12
left_border 				0
right_border 				0
top_border				0
bottom_border				95
set_avoid_player_start_areas
clumping_factor				4
}

create_terrain DIRT
{
  base_terrain                   DESERT
  number_of_clumps               16
  spacing_to_other_terrain_types 0
  land_percent                   10
  set_scale_by_size
}


if DESERT_MAP
  create_terrain PALM_DESERT
{
  base_terrain                   DESERT
  if TINY_MAP
  spacing_to_other_terrain_types 3
  else
  spacing_to_other_terrain_types 5
  endif
  land_percent                   10
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  if TINY_MAP
  spacing_to_other_terrain_types 3
  else
  spacing_to_other_terrain_types 5
  endif
  land_percent                   10
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

if DESERT_MAP
create_terrain FOREST
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius 18
  min_distance_to_players 0
  max_distance_to_players 0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 6
}

create_object SCOUT
{
  number_of_objects 1
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
}

create_object PALM_FOREST_TREE /* straggler trees */
{
  	number_of_objects             2
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       4
  	max_distance_to_players       5
  	min_distance_group_placement  2
}
create_object PALM_FOREST_TREE 
{
  	number_of_objects             3
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       6
 	max_distance_to_players       8
  	min_distance_group_placement  2
}

create_object SHEEP
{
   number_of_objects 4
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 9
   max_distance_to_players 11
}

create_object SHEEP
{
   number_of_objects 2
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 12
   max_distance_to_players 27
}

create_object SHEEP
{
   number_of_objects 2
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 12
   max_distance_to_players 27
}

create_object DEER
{
  number_of_objects 4
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 30
}

create_object BOAR
{
  number_of_objects 1
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 16
  max_distance_to_players 22
}

create_object BOAR
{
  number_of_objects 1
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 16
  max_distance_to_players 22
}

/* NEAR GOLD */
create_object GOLD
{
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    16
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* MEDIUM GOLD */
create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */ 
create_object GOLD
{
  number_of_objects          4
  number_of_groups           1
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       25
  max_distance_to_players       35
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */
create_object STONE
{
  number_of_objects          5
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */
create_object STONE 
{
  number_of_objects          4
  number_of_groups           1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       20
  max_distance_to_players       26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR FORAGE */
create_object FORAGE
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    12
  min_distance_group_placement  6
}

/* EXTRA GOLD */

if TINY_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

/* EXTRA STONE */

if TINY_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object STONE
{
  number_of_groups           2    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           3    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

create_object WOLF
  {
    number_of_objects          2
    set_gaia_object_only
    set_place_for_every_player
    min_distance_group_placement 12
    min_distance_to_players    30
    max_distance_to_players    40
  }

create_object WOLF
{
  number_of_groups          2
  set_loose_grouping
  number_of_objects 1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
}

/* Neutral Resources/Objects */

create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}

create_object PALMTREE
{
  number_of_objects         	 	50
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    		8
}

create_object SHORE_FISH
{
  number_of_objects                    8
  min_distance_group_placement         3
  set_gaia_object_only
  set_scaling_to_map_size
}

create_object SALMON
{
  number_of_objects                   15
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}

create_object MARLIN1
{
  number_of_groups                      3
  number_of_objects                     1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement        10 
  max_distance_to_other_zones         7
} 

create_object MARLIN2
{
  number_of_groups                      2
  number_of_objects                     1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement        10 
  max_distance_to_other_zones         7
} 

<CLIFF_GENERATION>

if TINY_MAP
	min_number_of_cliffs 5
	max_number_of_cliffs 7
	min_length_of_cliff 5
	max_length_of_cliff 8
	min_distance_cliff 20
elseif SMALL_MAP
	min_number_of_cliffs 9
	max_number_of_cliffs 11
	min_length_of_cliff 5
	max_length_of_cliff 8
	min_distance_cliff 20
elseif MEDIUM_MAP
	min_number_of_cliffs 13
	max_number_of_cliffs 15
	min_length_of_cliff 5
	max_length_of_cliff 8
	min_distance_cliff 20
elseif LARGE_MAP
	min_number_of_cliffs 17
	max_number_of_cliffs 19
	min_length_of_cliff 5
	max_length_of_cliff 8
	min_distance_cliff 20
elseif HUGE_MAP
	min_number_of_cliffs 21
	max_number_of_cliffs 23
	min_length_of_cliff 5
	max_length_of_cliff 8
	min_distance_cliff 20
elseif GIGANTIC_MAP
	min_number_of_cliffs 25
	max_number_of_cliffs 27
	min_length_of_cliff 5
	max_length_of_cliff 8
	min_distance_cliff 20
endif

<ELEVATION_GENERATION>
create_elevation        4
{
if DESERT_MAP
  base_terrain                DESERT
else           
  base_terrain                DIRT
endif
  number_of_clumps 		15
  start_random
  percent_chance 50
  number_of_tiles 		800
  percent_chance 50
  number_of_tiles 		1600
  end_random
  set_scale_by_groups
  set_scale_by_size           
}

create_elevation        4
{
if DESERT_MAP
  base_terrain                DESERT
else           
  base_terrain                DIRT
endif
  number_of_clumps 		15
  start_random
  percent_chance 50
  number_of_tiles 		800
  percent_chance 50
  number_of_tiles 		1600
  end_random
  set_scale_by_groups
  set_scale_by_size           
}
